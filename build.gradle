apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = "io.innerloop"
archivesBaseName = "guice-persist-neo4j"
version = "0.1.0"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile group: 'org.neo4j', name: 'neo4j-ogm-core', version: '2.0.4'
    compile 'com.google.inject.extensions:guice-persist:4.1.0'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Google Guice - Extensions - Persist - Neo4j'
                packaging 'jar'
                description 'Guice Persist module to work with Neo4J OGM: https://github.com/neo4j/neo4j-ogm'
                url 'https://github.com/inner-loop/guice-persist-neo4j'

                scm {
                    connection 'git@github.com:inner-loop/guice-persist-neo4j.git'
                    developerConnection 'git@github.com:inner-loop/guice-persist-neo4j.git'
                    url 'https://github.com/inner-loop/guice-persist-neo4j'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE, Version 2.0'
                        url 'http://www.gnu.org/licenses/gpl-2.0.html'
                    }
                }

                developers {
                    developer {
                        id 'mangrish'
                        name 'Mark Angrish'
                        email 'mark.angrish@innerloop.io'
                    }
                }
            }
        }
    }
}
